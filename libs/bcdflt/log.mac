	.Z80

	public	LOG,LOG10

	extrn	FLD1R,FSTOR,FMOVE,FADD,FMUL,FDIV,PLYEVL,XFORM,IFP
	extrn	FR0,FR1,?FLAG,?DIGRT,?FSCR,ILN10,FPhalf

	cseg

LOG:	ld	a,1
	jr	log_e

LOG10:	xor	a
log_e:	ld	(?FLAG),a	; ?FLAG: 0 = log10(x), 1 = ln(x)
	ld	a,(FR0)
	or	a
	scf
	ret	z		; error if FR0 <= 0
	ret	m
;;	ld	a,(FR0)
	ld	(FR1),a
	sub	40h		; subtract exponent bias
	add	a,a
	ld	(?DIGRT),a
	ld	a,(FR0+1)
	and	0F0h
	jr	nz,log1
	inc	a		; A=1
	jr	log2
log1:	ld	hl,?DIGRT
	inc	(hl)
	ld	a,10h
log2:	ld	(FR1+1),a
	ld	hl,FR1+2+4	; FR1+6 = FR2
	ld	b,5
	xor	a
log3:	ld	(hl),a		; clear down to FR1+2
	dec	hl
	djnz	log3

	call	FDIV
	ld	hl,SQR10	; sqr(10)
	call	XFORM		; FR0 = (FR0 - sqr(10)) / (FR0 + sqr(10))
	ld	hl,?FSCR
	call	FSTOR
	call	FMOVE
	call	FMUL
	ld	a,10
	ld	hl,LOGC		; point to coefficients
	call	PLYEVL		; evaluate polynom
	ld	hl,?FSCR
	call	FLD1R
	call	FMUL
	ld	hl,FPhalf	; 0.5
	call	FLD1R
	call	FADD
	call	FMOVE
	xor	a
	ld	(FR0+1),a
	ld	a,(?DIGRT)
	ld	(FR0),a
	or	a
	jp	p,log4
	neg
	ld	(FR0),a
log4:	call	IFP
	ld	a,(?DIGRT)
	or	a
	jp	p,log5
	ld	hl,FR0
	set	7,(hl)
log5:	call	FADD
	ld	a,(?FLAG)	; log10 or ln?
	or	a
	ret	z		; return with CY clear if log10
	ld	hl,ILN10
	call	FLD1R
	call	FDIV		; otherwise divide result by 1/ln(10)
	or	a		; clear CY
	ret

SQR10:	db	040h,003h,016h,022h,077h,066h	;  3.16227766 [sqr(10)]

	; logarithm coefficients

LOGC:	db	03Fh,049h,015h,057h,011h,008h	;  0.4915571108
	db	0BFh,051h,070h,049h,047h,008h	; -0.5170494708
	db	03Fh,039h,020h,057h,061h,095h	;  0.3920576195
	db	0BFh,004h,039h,063h,003h,055h	; -0.0439630355
	db	03Fh,010h,009h,030h,012h,064h	;  0.1009301264
	db	03Fh,009h,039h,008h,004h,060h	;  0.0939080460
	db	03Fh,012h,042h,058h,047h,042h	;  0.1242584742
	db	03Fh,017h,037h,012h,006h,008h	;  0.1737120608
	db	03Fh,028h,095h,029h,071h,017h	;  0.2895297117
	db	03Fh,086h,085h,088h,096h,044h	;  0.8685889644

	end
