	.Z80

WBOOT	equ	0
BDOS	equ	5

CR	equ	0Dh
LF	equ	0Ah
ESC	equ	1Bh

	public	PUTCH,PUTSTR,TTFLSH,INKEY,GETCH,DELAY
	public	INIT,EXIT,ADEC,HLDEC,UCASE,CRLF,CPHLDE

	cseg

INIT:	xor	a
	ld	(kstat),a
	ret

EXIT:	jp	WBOOT

PUTCH:	push	hl
	push	de
	push	bc
	ld	e,c
	ld	c,6
	call	BDOS
	pop	bc
	pop	de
	pop	hl
	ret

INKEY:	push	hl
	push	de
	push	bc
k0:	ld	e,0FFh
	ld	c,6
	call	BDOS
	or	a
	jr	z,k5
	ld	c,a
	ld	hl,kstat
	cp	3		; ^C
	jr	nz,k1
	ld	(hl),0
	jr	k4
k1:	cp	ESC
	jr	nz,k2
	ld	(hl),1
	jr	k0
k2:	ld	a,(hl)
	or	a
	jr	z,k4
	ld	a,c
	cp	'['
	jr	z,k0
	ld	(hl),0
k4:	ld	a,c
k5:	pop	bc
	pop	de
	pop	hl
	ret

GETCH:	call	INKEY
	or	a
	jr	z,GETCH
	ret

PUTSTR:	ld	a,(hl)
	or	a
	ret	z
	push	bc
	ld	c,a
	call	PUTCH
	pop	bc
	inc	hl
	jr	PUTSTR

TTFLSH:
	ret

DELAY:	ld	c,a
d0:	ld	b,60h		; speed
d1:	push	bc
	ld	bc,800h
d2:	dec	bc
	ld	a,b
	or	c
	jr	nz,d2
	pop	bc
	djnz	d1
	dec	c
	jr	nz,d0
	ret

ADEC:	push	de
	push	bc
	ld	d,0
	ld	b,100
	call	ad1
	ld	b,10
	call	ad1
	add	a,'0'
	ld	c,a
	call	PUTCH
	inc	d
	ld	a,d		; return length in A
	pop	bc
	pop	de
	ret

ad1:	ld	c,'0'-1
ad2:	inc	c
	sub	b
	jr	nc,ad2
	add	a,b
	push	af
	ld	a,c
	cp	'0'
	jr	nz,ad4
	inc	d
	dec	d
	jr	z,ad5
ad4:	call	PUTCH
	inc	d
ad5:	pop	af
	ret

HLDEC:	ld	(filler),a
	push	hl
	push	de
	push	bc
	ld	b,0
	ld	de,-10000
	call	sbcnt
	ld	de,-1000
	call	sbcnt
	ld	de,-100
	call	sbcnt
	ld	de,-10
	call	sbcnt
	ld	a,l
	add	a,'0'
	ld	c,a
	call	PUTCH
	inc	b
	res	7,b
	ld	a,b		; return length in A
	pop	bc
	pop	de
	pop	hl
	ret

sbcnt:	ld	c,'0'-1
sb1:	inc	c
	add	hl,de
	jr	c,sb1
	sbc	hl,de
	bit	7,b
	jr	nz,sb3
	ld	a,c
	cp	'0'
	jr	nz,sb2
	ld	a,(filler)
	or	a
	ret	z
	ld	c,a
	jr	sb3
sb2:	set	7,b
sb3:	call	PUTCH
	inc	b
	ret

UCASE:	cp	'a'
	ret	c
	cp	'z'+1
	ret	nc
	and	5Fh
	ret

CRLF:	push	bc
	ld	c,CR
	call	PUTCH
	ld	c,LF
	call	PUTCH
	pop	bc
	ret

; Returns Z if HL == DE, CY if HL > DE

CPHLDE:	ld	a,d
	cp	h
	ret	nz
	ld	a,e
	cp	l
	ret

	dseg

kstat:	ds	1
filler:	ds	1

	end
