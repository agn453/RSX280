;**********************************************************************;
;                                                                      ;
;   This file is part of RSX280, a multi-tasking OS for the Z280.      ;
;   Copyright (C) 1985-2022, Hector Peraza.                            ;
;                                                                      ;
;   This program is free software; you can redistribute it and/or      ;
;   modify it under the terms of the GNU General Public License as     ;
;   published by the Free Software Foundation; either version 2 of     ;
;   the License, or (at your option) any later version.                ;
;                                                                      ;
;   This program is distributed in the hope that it will be useful,    ;
;   but WITHOUT ANY WARRANTY; without even the implied warranty of     ;
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the      ;
;   GNU General Public License for more details.                       ;
;                                                                      ;
;   You should have received a copy of the GNU General Public License  ;
;   along with this program; if not, write to the Free Software        ;
;   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.          ;
;                                                                      ;
;**********************************************************************;

	.Z280

	ident	/6.29/

	include	DCB.INC
	include	TCB.INC
	include	PCB.INC
	include	SYSFN.INC
	include	ERRORS.INC

	include	SYSCONF.INC

	extrn	SYSSTK,MMUINI,INITKM,ININT,SETVEC,INTON,INTOFF
	extrn	TMRINT,$ASGN,SYSENT,SYSLVL,$PHYDV,$FNPAR,$ALLOC
	extrn	CLRMEM,UPTIME,NVREAD,LDRTCB,TRQST,$POLSZ,$PLIST
	extrn	$MFLGS,MSIZE,GO

BOOTDV	equ	0081h		; address of the boot device name set
				;  by the bootstrap loader

	cseg

;-----------------------------------------------------------------------

; This is the main System Startup routine.
; Initializes everything and starts a few essential tasks before
; passing control to the dispatcher.

START::	di
	ld	sp,SYSSTK	; use SYSSTK in common memory

	call	MMUINI		; init MMU

	ld	hl,($POLSZ)
	ld	a,h
	or	l
	jp	z,uncfg

	ld	hl,0
	ld	bc,128
	call	CLRMEM		; clear RST vector area

	ld	a,-1
	ld	(SYSLVL),a	; we are in system state

	call	NVREAD		; read machine-specific settings
	call	INITKM		; init task manager
	call	ININT		; init interrupt system
	call	INTOFF		; make sure interrupts are off

	ld	hl,UPTIME
	ld	bc,5
	call	CLRMEM		; init uptime

	ld	hl,$MFLGS
	set	7,(hl)		; set boot flag

	ld	hl,($PLIST)
p1:	ld	a,h
	or	l
	jr	z,nopar		; no partitions defined
p2:	ex	ix,hl		; get PCB address into IX
	ldw	hl,(ix+P.LNK)
	ld	a,h
	or	l		; last in list?
	jr	nz,p2		; no, keep searching
	bit	PA.SYS,(ix+P.ATTR)
	jr	z,nopar		; user controlled, can't resize
	call	MSIZE		; get system memory size, in Kbytes
	srl	h		; convert to pages
	rr	l
	srl	h
	rr	l
	subw	hl,(ix+P.BASE)
	jr	c,nopar		; partition base above upper RAM limit!
	ldw	(ix+P.SIZE),hl	; resize partition to use the remaining available space

nopar:	; initialize physical devices

	call	INTON		; enable timer interrupts to service
	ei			;  any possible device timeouts

	ld	hl,$PHYDV
inidev:	ldw	de,(hl)		; get DCB address into DE
	inc	hl
	inc	hl
	ld	a,d
	or	e		; end of list?
	jr	z,done		; exit loop if yes
	push	de
	ld	hl,D.EPT
	add	hl,de
	ldw	hl,(hl)		; get address of dispatch table
	call	(hl)		; call the driver's initialization entry point
	pop	hl
	jr	inidev

done:	; assign console output CO: to TT0: (TODO: only if CO: is not loaded)

  IF 0
	ld	de,'TT'
	ld	c,0		; 'TT0:' DE-C = physical
	ld	hl,'CO'
	ld	b,0		; 'CO0:' HL-B = logical (system console device)
	ld	a,N.GBL
	ld	ix,0
	call	$ASGN
  ENDIF

	ld	de,(BOOTDV)	; get boot device
	ld	a,(BOOTDV+2)
	ld	c,a
	call	$FNDEV##
;;	jp	c,...
	push	hl
	ld	hl,(DEVLB##+3)
	ld	de,U.RED
	add	hl,de
	pop	de
	ldw	(hl),de		; redirect LB: to boot device

	ld	de,'TT'
	ld	c,0
	call	$FNDEV
;;	jp	c,...
	push	hl
	ld	hl,(DEVCL##+3)
	ld	de,U.RED
	add	hl,de
	pop	de
	ldw	(hl),de		; redirect CL: to TT0:

	ld	a,0C3h
	ld	(0),a
	ld	hl,$DBTRP##
	ld	(1),hl

	ld	hl,0
	ld	(MCRTCB##),hl

	; start loader task - should have been installed and fixed in memory
	; by VMR

	ld	hl,LDRTDB
	call	?RQST		; request loader
	jp	c,noldr
	ld	(LDRTCB),ix

 if 1
	ld	hl,MCRTDB
	call	?RQST		; request MCR
	jp	c,skip
	ld	(MCRTCB),ix
	set	TA.PRV,(ix+T.ATTR)
	set	TA.CLI,(ix+T.ATTR)
	set	TA.MCR,(ix+T.ATTR)
 endif

skip:	; start initialization task

	ld	hl,SAVTDB
	call	?RQST		; request initialization task
	jp	c,uncfg

	ldw	hl,(ix+T.CTX)
	addw	hl,TX.REGS+20+2
	ldw	(hl),507Fh	; reset MSR
	inc	hl
	inc	hl
	ldw	de,(ix+T.EPT)
	ldw	(hl),de		; reset PC

	; start dispatcher

	jp	GO

?RQST:	call	TRQST
	ret	nc
	cp	E.TACT
	ret	z
	cp	E.TRUN
	ret	z
	scf
	ret

uncfg:	call	$DMTRP##
	db	'RSX280 -- System not configured',0
	di
	halt

noldr:	call	$DMTRP##
	db	'RSX280 -- Loader not installed',0
	di
	halt

;-----------------------------------------------------------------------

	dseg

	public	IDDTBL

IDDTBL:	GENDEV			; device-driver table from SYSCONF.INC
	dw	DEVTI##		; pseudo-device TI:
	dw	DEVCL##		; pseudo-device CL:
	dw	DEVLB##		; pseudo-device LB:
	dw	DEVSY##		; pseudo-device SY:
	dw	0		; end of table marker

LDRTDB:	db	'LDR...'	; task name
	dw	0,0		; addr and length of command line (not used)
	db	0,0,0		; attr, user, grp (not used)
	db	'CO',0		; terminal device
	dw	0,0		; addr and size of data blk to send (not used)

 if 1
MCRTDB:	db	'MCR...'	; task name
	dw	0,0		; addr and length of command line (not used)
	db	0,0,0		; attr, user, grp (not used)
	db	'CO',0		; terminal device
	dw	0,0		; addr and size of data blk to send (not used)
 endif

SAVTDB:	db	'...SAV'	; task name
	dw	0,0		; addr and length of command line (not used)
	db	0,0,0		; attr, user, grp (not used)
 if 0
	db	'CO',0		; terminal device
 else
	db	'TT',0		; terminal device
 endif
	dw	0,0		; addr and size of data blk to send (not used)

	end	START
